"
I represent an abstract packing system that is able to receive objects and pack them into a binary data (ByteArray).

Others can ask me for serializing an object by sending me #pack: or for materializing a ByteArray by sending me #unpack:. On the class side, you can find out #fuel and #ston methods, two available packing strategies. The method #version keeps current packing version and changelog. 

The main users are GTEventRecorder (for packing) and GTEventAnnouncement (for unpacking).

Public API and Key Messages

- pack:
- unpack:
- version

"
Class {
	#name : #GTEventPacking,
	#superclass : #Object,
	#category : #'GT-EventRecorder-Core'
}

{ #category : #'instance creation' }
GTEventPacking class >> default [
	^ self ston
]

{ #category : #'instance creation' }
GTEventPacking class >> fuel [
	^ GTFuelPacking new
]

{ #category : #'instance creation' }
GTEventPacking class >> ston [
	^ GTStonPacking new
]

{ #category : #private }
GTEventPacking >> materialize: aByteArray [
	^ self subclassResponsibility
]

{ #category : #private }
GTEventPacking >> newAnnouncementFor: aGTEventCollector [
	^ self 
		updateAnnouncement: aGTEventCollector newAnnouncement 
		withData: (self serialize: aGTEventCollector bundle).
]

{ #category : #packing }
GTEventPacking >> pack: aGTEventCollector [
	^ GTEventBundle new
			url: aGTEventCollector url;
			data: (self packCollector: aGTEventCollector);
			yourself
]

{ #category : #private }
GTEventPacking >> packCollector: aGTEventCollector [
	^ self serialize: (self newAnnouncementFor: aGTEventCollector)
]

{ #category : #private }
GTEventPacking >> serialize: anObject [
	^ self subclassResponsibility
]

{ #category : #packing }
GTEventPacking >> unpack: aByteArray [
	^ self materialize: aByteArray
]

{ #category : #private }
GTEventPacking >> updateAnnouncement: aGTEventAnnouncement withData: aByteArray [
	^ aGTEventAnnouncement
			data: aByteArray;
			serializedBy: self class name;
			eventRecorderVersion: self version;
			timestamp: DateAndTime now;
			imageHash: SmalltalkImage current imagePath hash;
			imageVersion: SystemVersion current version;
			latestUpdate: SystemVersion current highestUpdate;
			computerUUID: GlobalIdentifier uniqueInstance computerUUID asString;
			sessionUUID: SmalltalkImage current session sid;
			sessionCreationTime: SmalltalkImage current session creationTime;
			yourself
]

{ #category : #accessing }
GTEventPacking >> version [
	^ 3
	
"Changes to version 3:
	- GTSpotterEventRecorder rewritten to GTEventRecorder.
	- It uses STON instead of FUEL serializing engine.
		- FUEL engine is still available using GTFuelPacking object
Changes to version 2 (FUEL):
	- GTSpotterEventRecorder>>timestamp keeps DateAndTime object. 
	  Before, it was an integer as unix time.
"
]
