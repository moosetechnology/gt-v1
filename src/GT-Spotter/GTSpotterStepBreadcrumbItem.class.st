Class {
	#name : #GTSpotterStepBreadcrumbItem,
	#superclass : #GLMInteractableBrick,
	#instVars : [
		'labelBrick',
		'triangleBrick',
		'stepModel'
	],
	#category : #'GT-Spotter-UI'
}

{ #category : #actions }
GTSpotterStepBreadcrumbItem >> click: evt [
	super click: evt.
	
	self stepModel spotter popUntil: self stepModel.
	^ true
]

{ #category : #actions }
GTSpotterStepBreadcrumbItem >> deselect [
	
	self invalidateWith: [
		self labelBrick label customFont; beRegular ]
]

{ #category : #actions }
GTSpotterStepBreadcrumbItem >> hideTriangle [

	self triangleBrick color: Color transparent
]

{ #category : #initialization }
GTSpotterStepBreadcrumbItem >> initialize [
	super initialize.
	
	self
		useVerticalLinearLayout;
		labelBrick: self newLabelBrick;
		triangleBrick: self newTriangleBrick;
		enable
	
]

{ #category : #accessing }
GTSpotterStepBreadcrumbItem >> labelBrick [

	^ labelBrick
]

{ #category : #accessing }
GTSpotterStepBreadcrumbItem >> labelBrick: aBrick [

	labelBrick := aBrick.
	
	self themer spotterThemer breadcrumbItemLabelStyleFor: self labelBrick.
	self addBrickBack: self labelBrick
]

{ #category : #'instance creation' }
GTSpotterStepBreadcrumbItem >> newLabelBrick [

	^ GLMLabelBrick new
]

{ #category : #'instance creation' }
GTSpotterStepBreadcrumbItem >> newTriangleBrick [

	^ GLMBrick new
]

{ #category : #actions }
GTSpotterStepBreadcrumbItem >> select [
	
	self invalidateWith: [
		self labelBrick label customFont; beBold ]
]

{ #category : #actions }
GTSpotterStepBreadcrumbItem >> showTriangle [

	self triangleBrick color: self themer spotterThemer breadcrumbItemBackgroundColor
]

{ #category : #accessing }
GTSpotterStepBreadcrumbItem >> stepModel [
	^ stepModel
]

{ #category : #accessing }
GTSpotterStepBreadcrumbItem >> stepModel: anObject [
	stepModel := anObject
]

{ #category : #accessing }
GTSpotterStepBreadcrumbItem >> text: aString [

	self labelBrick text: aString
]

{ #category : #accessing }
GTSpotterStepBreadcrumbItem >> triangleBrick [

	^ triangleBrick
]

{ #category : #accessing }
GTSpotterStepBreadcrumbItem >> triangleBrick: aBrick [

	triangleBrick := aBrick.
	
	self themer spotterThemer breadcrumbItemTriangleStyleFor: self triangleBrick.
	self addBrickBack: self triangleBrick
]
